#
# qmake configuration for vxworks-simpentium-g++
#

MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = vxworks
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib
DEFINES                += VXWORKS

VXWORKS_ARCH            = simlinux
VXWORKS_CPU             = SIMLINUX
VXWORKS_ARCH_MUNCH      = pentium

include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)
include(../../common/linux.conf)

QMAKE_CC                = cc$$VXWORKS_ARCH_MUNCH
QMAKE_CFLAGS            = -fno-builtin -I$(WIND_BASE)/target/h -I$(WIND_BASE)/target/h/wrn/coreip -DCPU=$$upper($$VXWORKS_ARCH) -DVX_CPU_FAMILY=$$VXWORKS_ARCH -DTOOL_FAMILY=gnu -DTOOL=gnu -D_WRS_KERNEL -D_VSB_CONFIG_FILE=\'<../lib/h/config/vsbConfig.h>\'
QMAKE_CFLAGS_SHLIB      = # remove -fPIC
QMAKE_CFLAGS_STATIC_LIB = # remove -fPIC
QMAKE_CXX               = c++$$VXWORKS_ARCH_MUNCH
QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS
QMAKE_CXXFLAGS_SHLIB    = # remove -fPIC
QMAKE_CXXFLAGS_STATIC_LIB = # remove -fPIC
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX
QMAKE_LFLAGS_SHLIB      = -Wl,-Ur -L $(WIND_BASE)/target/lib/$$VXWORKS_ARCH/$$VXWORKS_CPU/common # remove -shared
QMAKE_LFLAGS_PLUGIN     = $$QMAKE_LFLAGS_SHLIB
QMAKE_LFLAGS_APP       += -Wl,-Ur -L $(WIND_BASE)/target/lib/$$VXWORKS_ARCH/$$VXWORKS_CPU/common
QMAKE_LIBS_NETWORK      = # -lnetwrap # only needed if kernel is missing gethostbyname() and friends
QMAKE_LIBS_DYNLOAD      =
QMAKE_LIBS_THREAD       =

load(qt_config)

