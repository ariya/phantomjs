#
# qmake configuration for the Freescale iMX53 board
#
# This mkspec is based and tested on the Yocto Project's Poky Distribution
# with libEGL.so from Freescale without the X11 dependency.
#
# This mkspec is tested with a framebuffer (eglfs) configuration
#
# A typical configure line looks like:
#
# export PATH=/opt/imx53qsb/x86_64-linux/usr/bin/armv7a-vfp-neon-poky-linux-gnueabi:$PATH
#
# ./configure -prefix /usr/local/qt5-imx53 -hostprefix /opt/imx53qsb/x86_64-linux/usr/local/qt5-imx53 -release -device linux-imx53qsb-g++
# -opensource -confirm-license -no-gtkstyle -device-option CROSS_COMPILE=arm-poky-linux-gnueabi- -sysroot /opt/imx53qsb/sysroot
# -eglfs -no-pch -opengl es2 -no-xcb -silent

include(../common/linux_device_pre.conf)

QMAKE_INCDIR           += $$[QT_SYSROOT]/usr/include
QMAKE_LIBDIR           += $$[QT_SYSROOT]/usr/lib

QMAKE_LIBS_EGL         += -lEGL
QMAKE_LIBS_OPENGL_ES2  += -lGLESv2 -lEGL
QMAKE_LIBS_OPENVG      += -lOpenVG -lEGL

QMAKE_LFLAGS           += -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib

IMX5_CFLAGS             = -march=armv7-a -mfpu=neon -DLINUX=1 -DEGL_API_FB=1 -Wno-psabi
QMAKE_CFLAGS           += $$IMX5_CFLAGS
QMAKE_CXXFLAGS         += $$IMX5_CFLAGS

include(../common/linux_arm_device_post.conf)

load(qt_config)
